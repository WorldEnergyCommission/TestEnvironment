<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="FB_Get_Data_SmartMeApi_ElectricMeter" Id="{b45d4e52-8b43-4435-925c-37e92fd1811f}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'hide_all_locals'}
FUNCTION_BLOCK FB_Get_Data_SmartMeApi_ElectricMeter
VAR_INPUT
	bStart						: BOOL;															//Start the connection to the Service
	sUsername					: STRING(50);													//Username from the smart-me Project
	sPassword					: STRING(50);													//Password from the smart-me Project
	tDelayRequest				: TIME := T#1S; 												//Delay between the Requests		
END_VAR
VAR_OUTPUT
	bValidResult				: BOOL;															//Valid Result received from Server
    bBusy						: BOOL;															//Function is Busy
    bError						: BOOL;															//Error sucess
	hrErrorCode       			: HRESULT;														//Error Result from Http Client
	arrListOfDevices			: ARRAY[1..50] OF ST_ListOfValues_SmartMe_ElectricMeter;		//List with all received Values from the Server
END_VAR
VAR
	fbHttpClientSmartMe			: FB_IotHttpClient :=(sHostName:='smart-me.com',
                                  bKeepAlive:=TRUE, tConnectionTimeout:=T#15S);					//Http Client Function
	fbHttpGet					: FB_HTTP_GetData_Smart_me;										//Http Get Request to the Smart Me Service
	fbJson						: FB_Decode_Json_SmartMe_ElectricMeter;							//Deconde received Json
	timStartRequest				: TON;															//Timer to Start the Request
	timDissableFunctions		: TON;															//Timer to dissable the Function after Lynus_Standards.Constants_General.tTimeDissableLynusFunctions Days without connection to the Lynus Cloud
	timResetConnectionOnGVL		: TON;															//Timer to try reset the connection Flag on the GVL. (When somebody delete the Connection Function and make only a onlinechange)
	timReset24					: TON;															//timer disconnect every 24h
	FPStartRequest				: R_TRIG;														//Internal positive Edge
	FNStart						: F_TRIG;														//Internal negative Edge
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[//Creator : Kai Ebensperger
//Company : Lynus AG
//Date : 29.09.2021
//Version : 1.0.0.0

//With this Function is possible to send a HTTP Get Request to the Smart-me System and recieve values from the Project wich is defined with the Username and the password
//With this Function is possible to decode only Data from Electric Meter in the Project
//Other Device Types or Meters are not supported and the Function deliver a Error
//Attetnion : If the Username is a Mailadress write all in small carakters. For example kai.ebensperger@ventus.ag

(*--------------------------------------------------------------------------------------------------------Limits---------------------------------------------------------------------------------------------------*)

tDelayRequest := LIMIT(T#1S,tDelayRequest,T#15M);

(*-------------------------------------------------------------Service from Backend is ready and check the connection to backen for dissabel/enable Function---------------------------------------------------------------*)

//Try to reset the variable for connection on the GVL. When all is normal then the Lynus Mqtt connection function set this variable to true in the next cycle
//When we have no connection to the backend then after Lynus_Standards.Constants_General.tTimeDissableLynusFunctions days the Function is dissabled with all of his functionalities
timResetConnectionOnGVL(IN:= NOT timResetConnectionOnGVL.Q, PT:= T#1H, Q=> , ET=> );
	IF timResetConnectionOnGVL.Q THEN Lynus_Standards.GVL_Communicator.bStateConnectionToEnableFunctions := FALSE; END_IF
		IF NOT Lynus_Standards.GVL_Communicator.bStateConnectionToEnableFunctions THEN timDissableFunctions.IN := TRUE; ELSE timDissableFunctions.IN := FALSE; END_IF  
			timDissableFunctions(IN:= , PT:= Lynus_Standards.Constants_General.tTimeDissableLynusFunctions, Q=> , ET=> );

(*------------------------------------------------------------------------------------Configure HTTP Client------------------------------------------------------------------------------------------------------*)

IF NOT fbHttpClientSmartMe.bConfigured THEN
    fbHttpClientSmartMe.nHostPort:= 443;
    fbHttpClientSmartMe.stTLS.bNoServerCertCheck:= TRUE;
END_IF

//Timer Disconnect to Server every 24H because somthing goes wrog with the loval http plc in the auhofcenter 
timReset24(IN:= bStart AND NOT timReset24.Q, PT:= T#24H, Q=> , ET=> );

//Timer to start the Request
FPStartRequest(CLK:= bStart, Q=> );
	timStartRequest(IN:= bStart AND NOT timStartRequest.Q AND NOT timDissableFunctions.Q AND NOT bBusy AND fbHttpClientSmartMe.bConfigured AND NOT timReset24.Q, PT:= tDelayRequest, Q=> , ET=> );
		IF timStartRequest.Q OR (FPStartRequest.Q AND NOT bBusy AND fbHttpClientSmartMe.bConfigured) THEN fbHttpGet.bSend := TRUE; END_IF

IF timReset24.Q THEN fbHttpGet.bSend := FALSE; END_IF				
		
//Http Get Function
fbHttpGet(
	bSend:= , 
	sUsername:= sUsername, 
	sPassword:= sPassword, 
	tTimeOut:= T#20S,
	fbClient:= fbHttpClientSmartMe, 
	bValidResult=> bValidResult, 
	bBusy=> bBusy, 
	bError=> , 
	sContent=> );
	
fbHttpGet.bSend := FALSE;
	
//Decode Json
fbJson(
	bExecute:= bValidResult, 
	sContent:= fbHttpGet.sContent, 
	arrListOfDevices=> , 
	bDone=> , 
	bError=> );

IF fbJson.bDone AND NOT fbJson.bError AND NOT bBusy THEN
	arrListOfDevices := fbJson.arrListOfDevices;
END_IF		
	
//Http Client
FNStart(CLK:= bStart, Q=> );
	IF FNStart.Q OR timReset24.Q THEN fbHttpClientSmartMe.Disconnect(); END_IF

fbHttpClientSmartMe.Execute();

//Error
IF fbHttpGet.bError OR fbJson.bError OR fbHttpClientSmartMe.bError THEN bError := TRUE; ELSE bError := FALSE; END_IF	
	hrErrorCode := fbHttpClientSmartMe.hrErrorCode;
]]></ST>
    </Implementation>
    <LineIds Name="FB_Get_Data_SmartMeApi_ElectricMeter">
      <LineId Id="249" Count="28" />
      <LineId Id="403" Count="0" />
      <LineId Id="405" Count="0" />
      <LineId Id="404" Count="0" />
      <LineId Id="278" Count="4" />
      <LineId Id="372" Count="0" />
      <LineId Id="375" Count="0" />
      <LineId Id="373" Count="0" />
      <LineId Id="284" Count="26" />
      <LineId Id="343" Count="1" />
      <LineId Id="342" Count="0" />
      <LineId Id="311" Count="4" />
      <LineId Id="47" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>